apply plugin: 'java'
apply plugin: 'maven'

/*
 * Simple date function
 */
def getDate() {
    def date = new Date()

    return date.format('yyyy-MM-dd-HH.mm.ss')
}

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    def stderr = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'status', '-s'
      standardOutput = stdout
      errorOutput = stderr
    }
    if (stdout.toString().length()>0) {
      return 'Private' + '-' + getDate()
    }
    exec {
      ignoreExitValue = true
      commandLine 'git', 'describe', '--tags'
      standardOutput = stdout
      errorOutput = stderr
    }
    if (stderr.toString().length()==0) {
      return stdout.toString().trim()
    } else {
      exec {
          commandLine 'git', 'log',  '--format=%h', '-n1'
          standardOutput = stdout
          errorOutput = stderr
      }
      return '0.0.0' + '-' + stdout.toString().trim()
    }
}


group = 'org.apache.mesos.kibana'
version = getVersionName()

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } // Include dependencies
    manifest {
        attributes 'Main-Class': 'org.apache.mesos.kibana.KibanaFramework'
    }
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile (
        [group:'org.apache.mesos', name: 'mesos',version:'0.22.1'],
        [group:'org.slf4j', name:'slf4j-api', version: '1.7.7'],
        [group:'org.apache.logging.log4j', name:'log4j-api', version: '2.0.2'],
        [group:'org.apache.logging.log4j', name:'log4j-core', version: '2.0.2'],
        [group:'org.apache.logging.log4j', name:'log4j-slf4j-impl', version: '2.0.2'])
    testCompile (
        [group:'org.mockito', name:'mockito-all', version: '1.10.19'],
        [group: 'junit', name: 'junit', version: '4.10'])
}
