apply plugin: 'java'
apply plugin: 'maven'

apply from: 'source.gradle'

task setVersion {
  description  'Defines the version from the state in git'
  dependsOn 'taGit'
  doFirst {
    version = getVersionName()
    println 'Version: ' + version
    // create a version.env file for Jenkins
    def envFile = new File('version.env')
    envFile.write 'VERSION=' + version +'\n'
  }
}

/*
 * When you build a new release
 * this task tags the repository
 *
 * gradle build -PtagRelease
 *
 */

task taGit {
  description 'Create an annotated tag on the current sha1 using the content of the file tag.txt'
  doFirst {
    def tagFile = new File('tag.txt')
    def tagLines = tagFile.readLines()
    assert 1 <= tagLines.size()
    println 'Using: git tag -a -F ' + tagFile + ' ' + tagLines[0]
    exec {
      commandLine 'git', 'tag', '-a', '-F', tagFile, tagLines[0]
    }
  }

}
taGit.onlyIf { project.hasProperty('tagRelease') }


group = 'org.apache.mesos.kibana'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

jar {
  dependsOn 'setVersion'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } } // Include dependencies
  manifest {
    attributes 'Main-Class': 'org.apache.mesos.kibana.KibanaFramework'
  }
}

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile (
        [group:'org.apache.mesos', name: 'mesos',version:'0.22.1'],
        [group:'org.slf4j', name:'slf4j-api', version: '1.7.7'],
        [group:'org.apache.logging.log4j', name:'log4j-api', version: '2.0.2'],
        [group:'org.apache.logging.log4j', name:'log4j-core', version: '2.0.2'],
        [group:'org.apache.logging.log4j', name:'log4j-slf4j-impl', version: '2.0.2'])
    testCompile (
        [group:'org.mockito', name:'mockito-all', version: '1.10.19'],
        [group: 'junit', name: 'junit', version: '4.10'])
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
